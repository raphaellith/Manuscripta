plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
    id("checkstyle")
    id("jacoco")
}

android {
    namespace = "com.manuscripta.student"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.manuscripta.student"
        minSdk = 27
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
        debug {
            enableUnitTestCoverage = true
            enableAndroidTestCoverage = true
        }
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        unitTests {
            isIncludeAndroidResources = true
            isReturnDefaultValues = true
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    // AndroidX
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.material)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.lifecycle.viewmodel)
    implementation(libs.androidx.lifecycle.livedata)

    // Room
    implementation(libs.androidx.room.runtime)
    ksp(libs.androidx.room.compiler)
    testImplementation("androidx.room:room-testing:2.6.1")

    // Retrofit & OkHttp
    implementation(libs.retrofit)
    implementation(libs.retrofit.gson)
    implementation(libs.okhttp.logging)

    // Hilt
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)

    // Testing
    testImplementation(libs.junit)
    testImplementation(libs.mockito.core)
    testImplementation(libs.robolectric)
    testImplementation(libs.androidx.arch.core.testing)
    testImplementation(libs.androidx.core)
    testImplementation(libs.hilt.android.testing)
    kspTest(libs.hilt.android.compiler)

    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
}

// Checkstyle configuration
checkstyle {
    toolVersion = "10.12.0"
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}

tasks.register<Checkstyle>("checkstyle") {
    source = fileTree("src/main/java")
    include("**/*.java")
    classpath = files()
}

// --- JACOCO CONFIGURATION (JAVA ONLY) ---

jacoco {
    toolVersion = "0.8.12"
}

tasks.register<JacocoReport>("jacocoTestReport") {
    dependsOn("testDebugUnitTest")

    reports {
        xml.required.set(true)
        html.required.set(true)
    }

    // 1. THE SOURCE OF TRUTH (JAVA)
    // This is where Android compiles your Java files.
    val javaClassesDir = "${layout.buildDirectory.get()}/intermediates/javac/debug/compileDebugJavaWithJavac/classes"

    // 2. THE FILTER
    val debugTree = fileTree(javaClassesDir) {
        // WHITELIST: Only look at files inside your package structure
        // This automatically drops 'dagger.hilt...', 'androidx...', etc.
        include("**/com/manuscripta/student/**")

        // BLACKLIST: Exclude generated noise that ends up INSIDE your package
        exclude("**/R.class",
            "**/ManuscriptaApplication.class",
            "**/ManuscriptaApplication$*.class",
            "**/ManuscriptaDatabase.class",
            "**/ManuscriptaDatabase$*.class",
            "**/DatabaseModule.class",
            "**/DatabaseModule$*.class",
            "**/ui/main/MainActivity.class",
            "**/ui/main/MainActivity$*.class",
            "**/R$*.class",
            "**/BuildConfig.*",
            "**/Manifest*.*",
            "**/*Test*.*",
            "android/**/*.*",
            "**/databinding/*",
            "**/BR.class",
            "**/*_Factory.class",
            "**/*_Factory$*.class",
            "**/*_MembersInjector.class",
            "**/*_HiltModules*.class",
            "**/Hilt_*.class",
            "**/*_Impl.class",
            "**/*_Impl$*.class",
            "**/*_GeneratedInjector.class"
        )
    }

    val mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files(mainSrc))
    classDirectories.setFrom(files(debugTree))

    // Point to the execution data generated by the test task
    executionData.setFrom(fileTree(layout.buildDirectory.get()) {
        include("outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec")
    })
}

tasks.register<JacocoCoverageVerification>("jacocoTestCoverageVerification") {
    dependsOn("jacocoTestReport")

    val javaClassesDir = "${layout.buildDirectory.get()}/intermediates/javac/debug/compileDebugJavaWithJavac/classes"

    val debugTree = fileTree(javaClassesDir) {
        include("**/com/manuscripta/student/**")
        exclude(
            "**/R.class",
            "**/ManuscriptaApplication.class",
            "**/ManuscriptaApplication$*.class",
            "**/ManuscriptaDatabase.class",
            "**/ManuscriptaDatabase$*.class",
            "**/DatabaseModule.class",
            "**/DatabaseModule$*.class",
            "**/ui/main/MainActivity.class",
            "**/ui/main/MainActivity$*.class",
            "**/R$*.class",
            "**/BuildConfig.*",
            "**/Manifest*.*",
            "**/*Test*.*",
            "android/**/*.*",
            "**/databinding/*",
            "**/BR.class",
            "**/*_Factory.class",
            "**/*_Factory$*.class",
            "**/*_MembersInjector.class",
            "**/*_HiltModules*.class",
            "**/Hilt_*.class",
            "**/*_Impl.class",
            "**/*_Impl$*.class",
            "**/*_GeneratedInjector.class"
        )
    }

    val mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files(mainSrc))
    classDirectories.setFrom(files(debugTree))

    executionData.setFrom(fileTree(layout.buildDirectory.get()) {
        include("outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec")
    })

    violationRules {
        rule {
            limit {
                minimum = "1.0".toBigDecimal() // 100% coverage
            }
        }
    }
}